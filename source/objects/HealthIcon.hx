package objects;

import backend.RecolorPreset;
import backend.RecolorUtil;

class HealthIcon extends FlxSprite {
	public var sprTracker:FlxSprite;

	private var isPlayer:Bool = false;
	private var char:String = '';

	public function new(char:String = 'face', isPlayer:Bool = false, ?allowGPU:Bool = true) {
		super();
		this.isPlayer = isPlayer;
		changeIcon(char, allowGPU);
		scrollFactor.set();
	}

	override function update(elapsed:Float) {
		super.update(elapsed);

		if (sprTracker != null)
			setPosition(sprTracker.x + sprTracker.width + 12, sprTracker.y - 30);
	}

	private var iconOffsets:Array<Float> = [0, 0];

	public function changeIcon(char:String, ?allowGPU:Bool = true) {
		if (this.char == char)
			return;

		var name:String = 'icons/' + char;
		if (!Paths.fileExists('images/' + name + '.png', IMAGE))
			name = 'icons/icon-' + char;
		if (!Paths.fileExists('images/' + name + '.png', IMAGE))
			name = 'icons/icon-face';

		var graphic = Paths.image(name, allowGPU);
		var iSize:Float = Math.round(graphic.width / graphic.height);
		loadGraphic(graphic, true, Math.floor(graphic.width / iSize), Math.floor(graphic.height));

		iconOffsets[0] = (width - 150) / iSize;
		iconOffsets[1] = (height - 150) / iSize;
		updateHitbox();

		animation.add(char, [for (i in 0...frames.frames.length) i], 0, false, isPlayer);
		animation.play(char);
		this.char = char;

		antialiasing = !char.endsWith('-pixel') && ClientPrefs.data.antialiasing;

		// =====================================
		// APPLY RECOLOR (ICON)
		// =====================================
var recolorPath = 'assets/shared/data/recolors/' + char + '.json';
var data = RecolorPreset.load(recolorPath);

if (data != null)
{
    RecolorUtil.recolor(this, data);
}

	}

	public var autoAdjustOffset:Bool = true;

	override function updateHitbox() {
		super.updateHitbox();
		if (autoAdjustOffset) {
			offset.x = iconOffsets[0];
			offset.y = iconOffsets[1];
		}
	}

	public function getCharacter():String {
		return char;
	}
}
